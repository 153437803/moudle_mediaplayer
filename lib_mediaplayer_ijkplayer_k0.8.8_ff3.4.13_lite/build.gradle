apply plugin: 'com.android.library'

android {

    namespace 'lib.kalu.ijkplayer'
    compileSdkVersion 32
    buildToolsVersion "32.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 32
        versionCode 100
        versionName "1.0.0"

        externalNativeBuild {
            cmake {
                //-fstack-protector：启用堆栈保护，不过只为局部变量中含有 char 数组的函数插入保护代码。
                //-fstack-protector-all：启用堆栈保护，为所有函数插入保护代码。
                //-fno-stack-protector：禁用堆栈保护。
                cFlags "-fstack-protector-all"
                cFlags "-fPIE"
                cppFlags "-std=c++11"
                cppFlags "-Wl,-Bsymbolic"
                version "3.18.1"
//                abiFilters 'armeabi-v7a'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }

        ndk {
//            abiFilters 'armeabi-v7a'
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        //添加此行
        flavorDimensions "default"
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            def type = new String(buildType.name).toLowerCase()
            def time = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08"))
            outputFileName = "lib_mediaplayer_ijk_k0.8.8_ff3.4.13_lite_" + type + "_" + time + ".aar"
        }
    }

    externalNativeBuild {
        cmake {
            path "/src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
    }

    buildTypes {
        release {
            minifyEnabled true
            consumerProguardFiles 'consumer-rules.pro'
            proguardFiles 'proguard-rules.pro', 'consumer-rules.pro'
        }
        debug {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig true
    }
    // aar方法参数名被混淆问题
    tasks.withType(JavaCompile) {
        options.compilerArgs += ["-g", "-parameters"]
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.7.1'
}
